\\ QINP Standard Library
\\ Memory

space std:
	define MIN_MEM_LEN_USE_ALIGN 32 \\ minimum length of memory to use aligned algorithms on

	\\ Copy data from one buffer to another
	\\ @param dest The destination buffer
	\\ @param src The source buffer
	\\ @param num The number of bytes to copy
	\\ @return The destination buffer
	void* memcpy(void* dest, void* src, u64 num)...

	\\ Set every byte in a buffer to a value
	\\ @param dest The destination buffer
	\\ @param val The value to set every byte to
	\\ @param num The number of bytes to set
	\\ @return The destination buffer
	void* memset(void* dest, u8 val, u64 num)...

	\\ Allocate a buffer of a given siz
	\\ @param size The size of the buffer to allocate
	\\ @return The allocated buffer (null if failed)
	void* malloc(u64 size)...		\\ Platform dependent implementation

	\\ Free a buffer
	\\ @param buffer The buffer to free
	void free(void* buffer)...		\\ Platform dependent implementation



	void* memcpy(void* dest, void* src, u64 len)!:
		u8* d = (u8*)dest
		u8* s = (u8*)src

		while len--:
			*d++ = *s++
		return dest

	void* memset(void* dest, u8 val, u64 len)!:
		void* end = (u8*)dest + len

		u8* d8 = (u8*)dest

		if len >= MIN_MEM_LEN_USE_ALIGN:
			while (u64)d8 % sizeof(u64) && (void*)d8 < end:
				*d8++ = val

			u64 v64 = val
			v64 |= (v64 << 8)
			v64 |= (v64 << 16)
			v64 |= (v64 << 32)

			u64* d64 = (u64*)d8
			while (void*)(d64 + 1) < end:
				*d64++ = v64
		
			d8 = (u8*)d64

		while (void*)d8 < end:
			*d8++ = val

		return dest

import.linux "linux/memory.qnp"
import.windows "windows/memory.qnp"