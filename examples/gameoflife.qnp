import "std.qnp"

define WIDTH 20
define HEIGHT 10

pack Grid:
	bool cells[WIDTH][HEIGHT]

Grid grids[2]

Grid* pMain = grids + 0
Grid* pBack = grids + 1

void flip():
	Grid* pTemp = pMain
	pMain = pBack
	pBack = pTemp

void printBorder():
	std::print("+")
	i64 i = 0
	while i++ < WIDTH:
		std::print("-")
	std::print("+\n")

void show():
	i64 i = 0
	printBorder()
	while i < HEIGHT:
		i64 j = 0
		std::print("|")
		while j < WIDTH:
			if pMain->cells[j][i]:
				std::print("#")
			else:
				std::print(" ")
			++j
		std::print("|\n")
		++i
	printBorder()

void clear():
	std::print("\r")
	i64 i = 0
	while i++ < HEIGHT + 2:
		std::print("\e[1F")

void update():
	i64 i = 0
	while i < WIDTH:
		i64 j = 0
		while j < HEIGHT:
			i64 count = 0
			i64 k = i-1
			while k < i+2:
				i64 l = j-2
				while ++l < j+2:
					if k < 0 || k >= WIDTH:
						continue
					if l < 0 || l >= HEIGHT:
						continue
					if k == i && l == j:
						continue
					\\if pMain->cells[k][l]:
					\\	++count
					count += pMain->cells[k][l]
				++k
			if pMain->cells[i][j]:
				pBack->cells[i][j] = (2 <= count && count <= 3)
			else:
				pBack->cells[i][j] = (count == 3)
			++j
		++i

grids[0].cells[0][2] = true
grids[0].cells[1][0] = true
grids[0].cells[1][2] = true
grids[0].cells[2][1] = true
grids[0].cells[2][2] = true

i64 n = 50
while --n > 0:
	show()
	update()
	std::sleep(100)
	clear()
	flip()
show()